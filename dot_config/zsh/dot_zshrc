zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*:default' menu select=1


# change zsh history folder
export HISTFILE="${XDG_STATE_HOME}/zsh_history"
export HISTSIZE=10000
export SAVESIZE=100000
# 不要なコマンドをhistoryから除外する
zshaddhistory() {
    local line="${1%%$'\n'}"
    [[ ! "$line" =~ "^(cd|jj?|lazygit|la|ll|ls|rm|rmdir)($| )" ]]
}

# TODO: move tolazyfile
case "$OSTYPE" in
    darwin*)
        (( ${+commands[gdate]} )) && alias date='gdate'
        (( ${+commands[gls]} )) && alias ls='gls'
        (( ${+commands[gdircolors]} )) && alias dircolors='gdircolors'
        (( ${+commands[gmkdir]} )) && alias mkdir='gmkdir'
        (( ${+commands[gcp]} )) && alias cp='gcp'
        (( ${+commands[gmv]} )) && alias mv='gmv'
        (( ${+commands[grm]} )) && alias rm='grm'
        (( ${+commands[gdu]} )) && alias du='gdu'
        (( ${+commands[ghead]} )) && alias head='ghead'
        (( ${+commands[gtail]} )) && alias tail='gtail'
        (( ${+commands[gsed]} )) && alias sed='gsed'
        (( ${+commands[ggrep]} )) && alias grep='ggrep'
        (( ${+commands[gfind]} )) && alias find='gfind'
        (( ${+commands[gdirname]} )) && alias dirname='gdirname'
        (( ${+commands[gxargs]} )) && alias xargs='gxargs'
        # replace rm command to trash
        (( ${+commands[trash]} )) && alias rm='trash'
    ;;
esac


### ripgrep ###
export RIPGREP_CONFIG_PATH="$XDG_CONFIG_HOME/ripgrep/config"

### man with bat
export MANPAGER="sh -c 'col -bx | bat --theme='Dracula' -l man -p'"

alias ll='ls -lGF'
alias ls='ls -GF'
alias air="BluetoothConnector --connect 0c-3b-50-68-b4-73 --notify"
alias vi="nvim"
alias nv="nvim"
alias lg="lazygit"

# eza コマンド用のエイリアス設定
# exaコマンドはEOF
if [[ $(command -v eza) ]]; then
  alias e='eza --icons --git'
  alias l=e
  alias ls=e
  alias ea='eza -a --icons --git'
  alias la=ea
  alias ee='eza --time-style=long-iso -aahl --icons --git'
  alias ll=ee
  alias et='eza -T -L 3 -a -I "node_modules|.git|.cache" --icons'
  alias lt=et
  alias eta='eza -T -a -I "node_modules|.git|.cache" --color=always --icons | less -r'
  alias lta=eta
fi

# 編集が多いもの
alias zshrc="vi ${ZDOTDIR}/.zshrc && source ${ZDOTDIR}/.zshrc"

if type brew &>/dev/null; then
  FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH

  eval "$(starship init zsh)"
  eval "$(zoxide init zsh)"

  export LG_CONFIG_FILE="${HOME}/.config/lazygit/override.yml"
fi

# fzf関連の設定追加
if type fzf &>/dev/null && type tmux &>/dev/null; then
  export FZF_CTRL_T_COMMAND='rg --files --hidden --follow --glob "!.git/*"'
  export FZF_CTRL_T_OPTS="
      --preview 'bat  --color=always --theme='Dracula' --style=header,grid --line-range :100 {}'
  "
  [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
  # FZF使用時はtmuxでPOPUP
  export FZF_TMUX=1
  export FZF_TMUX_OPTS="-p 80%"
  export FZF_DEFAULT_OPTS="--layout=reverse --inline-info"
fi

# for emacs
bindkey -e

# fbr - checkout git branch
function fzf-checkout-branch() {
  local branches branch
  branches=$(git branch | sed -E 's/\* |  //g' | cut -d " " -f 1) &&
  branch=$(echo "$branches" | fzf-tmux -p 80% --preview "git show --color=always {}") &&
  git checkout "$branch"
  zle accept-line
}
zle     -N   fzf-checkout-branch
bindkey "^b" fzf-checkout-branch

# zoxideをショートカットで使用するためラップ
functions __wrap_zoxide(){
  zi
  zle accept-line
}
zle -N __wrap_zoxide
bindkey "^k" __wrap_zoxide

# TODO: move to lazyfile
export NODE_REPL_HISTORY="$XDG_STATE_HOME/node_history"
export SQLITE_HISTORY="$XDG_STATE_HOME/sqlite_history"
export MYSQL_HISTFILE="$XDG_STATE_HOME/mysql_history"
export PSQL_HISTORY="$XDG_STATE_HOME/psql_history"
# irb settings
export IRBRC="$XDG_CONFIG_HOME/irb/irbrc"

export EDITOR="nvim"

export NAVI_CONFIG="$XDG_CONFIG_HOME/navi/config.yaml"

function widget::ghq::yuki() {
  local project dir repository session current_session
  dir=$(ghq list -p | sed -e "s|${HOME}|~|" | fzf-tmux -p 80% --prompt='repo> ' --preview "bat --color=always --style=numbers \$(eval echo {})/README.md" --bind ctrl-d:preview-page-down,ctrl-u:preview-page-up)

  if [[ $dir == "" ]]; then
    return 1
  fi

  if [[ ! -z ${TMUX} ]]; then
    repository=${dir##*/}
    session=${repository//./-}
    current_session=$(tmux list-sessions | grep 'attached' | cut -d":" -f1)

    if [[ $current_session =~ ^[0-9]+$ ]]; then
      eval cd "${dir}"
      tmux rename-session $session
    else
      tmux list-sessions | cut -d":" -f1 | grep -e "^${session}\$" > /dev/null
      if [[ $? != 0 ]]; then
        tmux new-session -d -c $(eval echo "${dir}") -s $session
        tmux switch-client -t $session
      else
        eval cd "${dir}"
        zle accept-line
      fi
    fi
  else
    eval cd "${dir}"
  fi
}
zle -N widget::ghq::yuki

# C-g で呼び出せるようにする
bindkey "^]" widget::ghq::yuki


eval "$(sheldon source)"
